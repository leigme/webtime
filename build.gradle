buildscript {
    ext {
        springBootVersion = '2.0.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("se.transmode.gradle:gradle-docker:1.2")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'application'
apply plugin: 'docker'

group = 'me.leig.webtime'
version = '1.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = "me.leig.webtime.Application"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-web
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.0.3.RELEASE'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// 需要添加到docker镜像的jar包
jar {
    baseName = 'webtime'
    version = '1.0'
}

// 默认镜像
distDocker {
    baseImage = 'openjdk'
    maintainer = 'harrison'
}

// 添加jar到镜像的任务
task dockerBuilder(type: Docker) {
    applicationName = jar.baseName
    tagVersion = jar.version
    volume('/tmp')
    addFile("${jar.baseName}-${jar.version}.jar", "app.jar")
    entryPoint(["java", "-Djava.security.egd=file:/dev/./urandom", "-jar", 'app.jar'])
    exposePort(8999)
    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}